I Queries for KG
# 1. Articles discussing amphotericin B and COVID-19
Paragraphs that mention a certain active substance and a certain disorder, and the name and source of the corresponding paper.
Instances: amphotericin B , Disorder: COVID-19 MESHCode C000657245


PREFIX d4covid:<https://w3id.org/def/DRUGS4COVID19#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>

SELECT  DISTINCT ?section ?titleDisease ?paperTitle ?completePaper
WHERE {
    ?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
        ?paper rdfs:seeAlso ?completePaper .    
        ?paragraph d4covid:mentions ?activeSubstance .
        ?activeSubstance skos:prefLabel  'amphotericin B'@en  . 
        ?paragraph d4covid:mentions ?disease .
        ?disease a d4covid:Disease .
        ?disease d4covid:MESHCode 'C000657245' .   
        ?disease dc:title ?titleDisease .
             
} 








#2 Articles discussing drugs of the amphotericin B family (same ATC4) and COVID-19
Paragraphs that mention drugs of the amphotericin B family (same ATC4) and COVID-19, and the title and source of the corresponding paper.

PREFIX d4covid:<https://w3id.org/def/DRUGS4COVID19#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>

SELECT  DISTINCT ?section ?titleDisease ?paperTitle ?completePaper ?labelFamilySubstance
WHERE
{
    ?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
        ?paper rdfs:seeAlso ?completePaper .    
        ?paper d4covid:mentions ?familySubstance .
        ?familySubstance skos:prefLabel ?labelFamilySubstance .
        ?activeSubstance rdfs:subClassOf ?familySubstance .      
        ?activeSubstance skos:prefLabel  'amphotericin B'@en  . 
        ?paragraph d4covid:mentions ?disease .
        ?disease a d4covid:Disease .
        ?disease d4covid:MESHCode 'C000657245' .   
        ?disease dc:title ?titleDisease .
             
} 

    




# 3. Immunostimulants related to COVID-19
Documents that mention other immunostimulants in ATC (ATC code L03AX) and all of the ATCs in lower levels and the disease COVID-19 , the title and source of the corresponding paper.
Motivation: Documents that mention the BCG vaccine (ATC code L03AX03) (vaccine against the tuberculosis Bacteria), because there are studies that link the vaccine with partial immunization to COVID-19. 


PREFIX d4covid:<https://w3id.org/def/DRUGS4COVID19#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>

SELECT  DISTINCT ?section ?titleDisease ?paperTitle ?completePaper ?labelATCLevel5
{
    {?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
        ?paper rdfs:seeAlso ?completePaper .    
        ?paper d4covid:mentions ?activeSubstance .
        ?activeSubstance skos:notation 'L03AX'^^xsd:string . 
        ?paragraph d4covid:mentions ?disease .
        ?disease a d4covid:Disease .
        ?disease d4covid:MESHCode 'C000657245' .   
        ?disease dc:title ?titleDisease .   }
UNION
{
        ?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
        ?paper rdfs:seeAlso ?completePaper .    
        ?paper d4covid:mentions ?L5LevelSubstance .
        ?activeSubstance skos:notation 'L03AX'^^xsd:string . 
        ?L5LevelSubstance  rdfs:subClassOf ?activeSubstance .
        ?L5LevelSubstance skos:prefLabel ?labelATCLevel5 .
        ?paragraph d4covid:mentions ?disease .
        ?disease a d4covid:Disease .
        ?disease d4covid:MESHCode 'C000657245' .   
        ?disease dc:title ?titleDisease . }
}





#4. Immunostimulants related to COVID-19? (ordered by specificity)
Documents that mention other immunostimulants in ATC (ATC code L03AX) and all of the ATCs in lower levels and the disease COVID-19 , the title and source of the corresponding paper.
With ranking: the documents that contain the more specific ATC will appear first.
PREFIX d4covid:<https://w3id.org/def/DRUGS4COVID19#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>

SELECT  DISTINCT ?section ?titleDisease ?paperTitle ?completePaper ?labelATCLevel5
{
    {?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
        ?paper rdfs:seeAlso ?completePaper .    
        ?paper d4covid:mentions ?activeSubstance .
        ?activeSubstance skos:notation 'L03AX'^^xsd:string . 
        ?paragraph d4covid:mentions ?disease .
        ?disease a d4covid:Disease .
        ?disease d4covid:MESHCode 'C000657245' .   
        ?disease dc:title ?titleDisease .   }
UNION
{
        ?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
        ?paper rdfs:seeAlso ?completePaper .    
        ?paper d4covid:mentions ?L5LevelSubstance .
        ?activeSubstance skos:notation 'L03AX'^^xsd:string . 
        ?L5LevelSubstance  rdfs:subClassOf ?activeSubstance .
        ?L5LevelSubstance skos:prefLabel ?labelATCLevel5 .
        ?paragraph d4covid:mentions ?disease .
        ?disease a d4covid:Disease .
        ?disease d4covid:MESHCode 'C000657245' .   
        ?disease dc:title ?titleDisease . }
BIND (IF(BOUND(?L5LevelSubstance),5,0)+IF(BOUND(?activeSubstance),1,0)AS ?score)
}
ORDER BY DESC(?score)




#5. Diseases related to drugs that combine  immunosuppressant  and antimalarial activities with macrolide antibiotics
Paragraphs with drugs that combine immunosuppressant  and antimalarial activities with macrolide antibiotics and the diseases that appear in these document.
The drugs that combine  immunosuppressant  and antimalarial activities have the active ingredient P01BA02.
The macrolide antibiotics belong to the therapeutic group  J01FA;


PREFIX d4covid:<https://w3id.org/def/DRUGS4COVID19#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?section ?paperTitle ?notation2 ?titleDisease
WHERE {

        ?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
        ?paragraph d4covid:mentions ?activeSubstance1 .
        ?paragraph d4covid:mentions ?activeSubstance2 .
        ?activeSubstance1 skos:notation "P01BA02"^^xsd:string .
        ?activeSubstance2 skos:notation  ?notation2 .
        ?paragraph d4covid:mentions ?disease .
        ?disease a d4covid:Disease .
        ?disease d4covid:MESHCode 'C000657245' .   
        ?disease dc:title ?titleDisease . 
       FILTER (STRSTARTS(?notation2,"J01FA"))
}



          

#6.  Active substances discussed with Pneumonia and COVID-19
Paragraphs that mention pneumonia and COVID-19, the active ingredients that are mentioned in these paragraphs and the title in their corresponding papers. 



PREFIX d4covid:<https://w3id.org/def/DRUGS4COVID19#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?section ?paperTitle ?titleDisease ?labelSubstance
WHERE {
        ?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
         ?paragraph d4covid:mentions ?disease1 .
         ?disease1 a d4covid:Disease .
         ?disease1 d4covid:MESHCode 'C000657245' .   
         ?paragraph d4covid:mentions ?disease2 .
         ?disease2 a d4covid:Disease .
         ?disease2 dc:title ?titleDisease .
         ?paragraph  d4covid:mentions ?activeSubstance .        
         ?activeSubstance skos:prefLabel ?labelSubstance .
     FILTER(CONTAINS(?titleDisease,"Pneumonia") || CONTAINS(?titleDisease,"pneumonia"))
}











#7. Active substances discussed with coagulation activation and COVID-19
Paragraphs that mention the disorders coagulation activation and COVID-19, the active ingredients that appear in these paragraphs and the corresponding paper title.
Query proposed by a medical doctor. There is a need to block coagulation activation of patients with COVID-19. Necrosis problems have been observed in patients with COVID-19.


PREFIX d4covid:<https://w3id.org/def/DRUGS4COVID19#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?section ?paperTitle ?titleDisease ?labelSubstance

WHERE {
        ?paragraph a d4covid:Paragraph .
        ?paragraph d4covid:section ?section .
        ?paper d4covid:contains ?paragraph .
        ?paper dc:title ?paperTitle . 
         ?paragraph d4covid:mentions ?disease1 .
         ?disease1 a d4covid:Disease .
       #  ?disease1 d4covid:MESHCode 'C000657245' .   
         ?paragraph d4covid:mentions ?disease2 .
         ?disease2 a d4covid:Disease .
       # ?disease1 d4covid:MESHCode 'D001778' . 
         ?disease2 dc:title ?titleDisease .
         ?paragraph  d4covid:mentions ?activeSubstance .        
         ?activeSubstance skos:prefLabel ?labelSubstance .
     FILTER(CONTAINS(?titleDisease,"Coagulation") || CONTAINS(?titleDisease,"coagulation"))
}



2. Future queries for KG with crowd annotations.


#1. Diseases that  are contraindicated by drugs that combine  immunosuppressant  and antimalarial activities with macrolide antibiotics mentioned
Paragraphs with drugs that combine immunosuppressant  and antimalarial activities with macrolide antibiotics, the disorders  that are treated by these drugs, the disorders for which these drugs are counterindicated, and the title and problem of the corresponding paper. 
Query #3 with annotations on drugs that treat disorders and diseases counterindicated by these drugs.
Similarly Query #4, 5 and 6 could also be improved with further annotations. 




# 2.  Diseases that are contraindicated by drugs used to treat the symptoms of COVID-19 (e.g. fever, dry cough and respiratory impairments)
Which are the disorders that are counterindicated by the active ingredients used to treat fever, dry cough and respiratory impairments.
Query proposed by a general user


#3. Drugs used against COVID-19 that include corticoids 
Which are the drugs that include corticoids used against COVID-19 that have as effect the CPOD (Chronic Obstructive Pulmonary Disease) lung disease.
This query was proposed by a medical doctor. It is of interest because corticids are applied as treatment for COVID-19 and doctors want to know if the patient in the future will have the CPOD disease.


#4. Effects of hidroxicloriquina
 Which are the effects that may be produced by the active principle Hidroxicloriquina
Query proposed by a medical doctor Guides the doctor to know previous tests that should be done to the patient before applying the treatment, that could predict that those effects could take place.